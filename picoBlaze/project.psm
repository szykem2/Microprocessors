MEM "project.mem"                                      ; dump memory
; open ports
lcd              DSIO       $30
lcd_control      DSIO       $31
int_status       DSIO       $E0
int_mask         DSIO       $E1
ps2              DSIO       $70
ps2_int_edge     DSIO       $71
ps2_int_value    DSIO       $72
ps2_int_mask     DSIO       $73
led              DSIO       00

; rename registers
; registers sF, sE, sD are reserved for delays
shift            EQU        sC                         ; shift pressed
caps             EQU        sB                         ; caps lock
num              EQU        sA                         ; num lock
btn_rel          EQU        s9                         ; button released
int_cnt          EQU        s8                         ; interrupt counter
decoded          EQU        s7                         ; decoded value
cursor           EQU        s6                         ; cursor position
special          EQU        s5                         ; special character enter, home etc.

EINT                                                   ; enable interrupt system

LOAD    s0,      97
STORE   s0,      $1C
FETCH   s1,      $1C
OUT     s1,      led

; global interrupts
LOAD    s0,      2                                     ; prepare interrupt for PS/2
OUT     s0,      int_mask                              ; enable intterupts

; local interrupts
LOAD    s0,      3                                     ; native state of voltage line(high)
OUT     s0,      ps2
LOAD    s0,      1                                     ; edge interrupt for clock
OUT     s0,      ps2_int_edge                          ; set interrupting on edge
LOAD    s0,      0
OUT     s0,      ps2_int_value                         ; set interrupting on decreasing edge
LOAD    s0,      1                                     ; prepare interrupt mask for clock
OUT     s0,      ps2_int_mask                          ; enable interrupt for PS/2

; initialize display
LOAD    s0,      $38

OUT     s0,      lcd
CALL    handleE
CALL    delay5m

OUT     s0,      lcd
CALL    handleE
CALL    delay40u
CALL    delay40u
CALL    delay40u

OUT     s0,      lcd
CALL    handleE
CALL    delay40u

OUT     s0,      lcd
CALL    handleE
CALL    delay40u

LOAD    s0,      $06
OUT     s0,      lcd
CALL    handleE
CALL    delay40u

LOAD    s0,      $0e
OUT     s0,      lcd
CALL    handleE
CALL    delay40u

LOAD    s0,      $01
OUT     s0,      lcd
CALL    handleE
CALL    delay5m
CALL    fLine

LOAD s8, 0
LOAD sA, 0

et:
        JUMP     et

delay1m:
        LOAD     sF,      26

wait1m:
        CALL     delay40u
        SUB      sF,      1
        JUMP     NZ,      wait1m
        RET

delay5m:
        CALL     delay1m
        CALL     delay1m
        CALL     delay1m
        CALL     delay1m
        CALL     delay1m
        RET

delay1u:
        LOAD     sE,      24

wait1u:
        SUB      sE,      1
        JUMP     NZ,      wait1u
        LOAD     sE,      sE
        RET

delay40u:
        LOAD     sD,      36

wait40u:
        CALL     delay1u
        SUB      sD,      1
        JUMP     NZ,      wait40u
        RET

secLine:
        LOAD     s0,      $C0
        OUT      s0,      lcd
        CALL     handleE
        CALL     delay40u
        RET

fLine:
        LOAD     s0,      $80
        OUT      s0,      lcd
        LOAD     s4,      0
        CALL     handleE
        CALL     delay40u
        RET

handleE:
        LOAD     sE,      1
        OUT      sE,      lcd_control
        LOAD     sE,      0
        LOAD     s0,      s0
        OUT      sE,      lcd_control
        RET

handleERS:
        LOAD     sE,      3
        OUT      sE,      lcd_control
        LOAD     sE,      2
        LOAD     s0,      s0
        OUT      sE,      lcd_control
        RET

handleInt:
        ADD      int_cnt, 1
        COMP     int_cnt, 1
        JUMP     Z,       ed

        COMP     int_cnt, 10
        JUMP     Z,       ed

        COMP     int_cnt, 11
        JUMP     Z,       display
        
        IN       s1,      ps2
        SR0      s1
        COMP     s1,      1
        JUMP     NZ,      mov0
        SR1      decoded
        JUMP     ed
mov0:
        SR0      decoded
        JUMP     ed
display:
        COMP     btn_rel, 1
        JUMP     Z,       btnReleased
        COMP     decoded, $F0
        JUMP     Z,       released
        COMP     decoded, $E0
        JUMP     Z,       specialKey
        COMP     decoded, $58
        JUMP     Z,       capsLock
        
        FETCH    s4,      decoded
        COMP     s4,      0
        JUMP     Z,       ed
        CALL     upperCase
        
        OUT      s4,      led
        OUT      s4,      lcd
        CALL     handleERS
        CALL     delay40u
        LOAD     decoded, 0
        LOAD     int_cnt, 0
        JUMP     ed
specialKey:
        LOAD    special, 1
        JUMP     ed
capsLock:
        XOR      caps, 1
        JUMP     ed
btnReleased:
        COMP     shift,   1
        JUMP     NZ,       ed
        LOAD     shift,   0
        LOAD     btn_rel, 0
        LOAD     special, 0
        JUMP     ed
released:
        LOAD     btn_rel, 1
ed:
        LOAD     s3,      0
        OUT      s3,      int_status
        RETI     ENABLE

upperCase:
        ; TODO: implement procedure
        ; if shift or fla
        RET
        
        
ORG     $3FF
JUMP    handleInt
